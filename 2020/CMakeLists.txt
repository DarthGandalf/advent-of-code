cmake_minimum_required(VERSION 3.17)
project(AoC2020 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED yes)
list(APPEND CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake")
add_subdirectory(fmt-7.1.3 EXCLUDE_FROM_ALL)
enable_testing()
list(APPEND CMAKE_CTEST_ARGUMENTS "--output-on-failure")
set(js_days "")
set(last_day "")

function(aoc_executable num fullname visual)
	set(name "day${num}")
	add_library(${name}_lib ${name}.cpp)
	add_executable(${name} empty.cpp)
	target_link_libraries(${name}_lib fmt::fmt)
	target_link_libraries(${name} common ${name}_lib common)
	set(last_day ${num} PARENT_SCOPE)
	if(EMSCRIPTEN)
		target_link_libraries(${name} "--preload-file ${CMAKE_SOURCE_DIR}/input/2020/${name}.txt@input.txt")
		target_link_libraries(${name} "--preload-file ${CMAKE_SOURCE_DIR}/sprites@sprites")
		target_link_libraries(${name} "--preload-file ${CMAKE_SOURCE_DIR}/SynchronizerNbpRegular-Zgpz.ttf@SynchronizerNbpRegular-Zgpz.ttf")
		target_compile_options(${name}_lib PUBLIC "-sUSE_SDL=2")
		target_compile_options(${name}_lib PUBLIC "-sUSE_SDL_TTF=2")
		target_compile_options(${name}_lib PUBLIC "-sUSE_SDL_IMAGE=2")
		set(js_days "${js_days}[${num}, '${fullname}', ${visual}],\n" PARENT_SCOPE)
	else()
		target_link_libraries(${name}_lib SDL2::SDL2
			PkgConfig::SDL2IMAGE
			PkgConfig::SDL2TTF)
		add_test(${name}_test bash -c "diff -u <(cat ${CMAKE_SOURCE_DIR}/answers/${name}.txt) <(env VISUAL_SPEED=- ${CMAKE_BINARY_DIR}/${name})")
	endif()
endfunction()

add_library(common common.cpp)
target_link_libraries(common fmt::fmt)

if(EMSCRIPTEN)
	include_directories(${CMAKE_SOURCE_DIR})
	target_sources(common PRIVATE web.cpp)
	target_link_libraries(common "-s USE_SDL=2")
	target_link_libraries(common "-s USE_SDL_TTF=2")
	target_link_libraries(common "-s USE_SDL_IMAGE=2")
	target_link_libraries(common "-s SDL2_IMAGE_FORMATS='[\"png\"]'")
	target_link_libraries(common "-s ASYNCIFY")
	target_link_libraries(common "--bind")
	target_compile_options(common PUBLIC "-sUSE_SDL=2")
	target_compile_options(common PUBLIC "-sUSE_SDL_TTF=2")
	target_compile_options(common PUBLIC "-sUSE_SDL_IMAGE=2")

	# https://stackoverflow.com/questions/57358039/how-to-see-the-underlying-compiler-linker-command-line-with-cmake-nmake
	set(CMAKE_CXX_USE_RESPONSE_FILE_FOR_INCLUDES 0 FORCE)
	set(CMAKE_CXX_USE_RESPONSE_FILE_FOR_LIBRARIES 0 FORCE)
	set(CMAKE_CXX_USE_RESPONSE_FILE_FOR_OBJECTS 0 FORCE)
else()
	target_sources(common PRIVATE native.cpp)
	find_package(SDL2 REQUIRED)
	find_package(PkgConfig REQUIRED)
	pkg_check_modules(SDL2IMAGE REQUIRED IMPORTED_TARGET SDL2_image)
	pkg_check_modules(SDL2TTF REQUIRED IMPORTED_TARGET SDL2_ttf)
	target_link_libraries(common SDL2::SDL2
		PkgConfig::SDL2IMAGE
		PkgConfig::SDL2TTF)
	include(CMakeRC)
endif()

aoc_executable(1 "Report Repair" 0)
aoc_executable(2 "Password Philosophy" 0)
aoc_executable(3 "Toboggan Trajectory" 1)
aoc_executable(4 "Passport Processing" 0)
aoc_executable(5 "Binary Boarding" 0)
aoc_executable(6 "Custom Customs" 0)
aoc_executable(7 "Handy Haversacks" 0)
aoc_executable(8 "Handheld Halting" 0)
aoc_executable(9 "Encoding Error" 0)
aoc_executable(10 "Adapter Array" 0)
aoc_executable(11 "Seating System" 0)
aoc_executable(12 "Rain Risk" 1)

foreach(current_day RANGE 1 ${last_day})
	configure_file(day.html day${current_day}.html)
endforeach()

if(EMSCRIPTEN)
	target_compile_options(day4_lib PRIVATE "-sDISABLE_EXCEPTION_CATCHING=0")
	target_link_libraries(day4_lib "-s DISABLE_EXCEPTION_CATCHING=0")
	target_compile_options(day11_lib PRIVATE "-sDISABLE_EXCEPTION_CATCHING=0")
	target_link_libraries(day11_lib "-s DISABLE_EXCEPTION_CATCHING=0")
	file(WRITE ${CMAKE_BINARY_DIR}/alldays.js "export const days = [\n${js_days}];\n")
else()
	file(GLOB input_files
		"${CMAKE_SOURCE_DIR}/sprites/*.png"
		"${CMAKE_SOURCE_DIR}/input/2020/*.txt"
		"${CMAKE_SOURCE_DIR}/SynchronizerNbpRegular-Zgpz.ttf")
	cmrc_add_resource_library(inputs ${input_files})
	target_link_libraries(common inputs)

	add_executable(example_test example_test.cpp common.cpp)
	target_link_libraries(example_test day${last_day}_lib)
endif()
